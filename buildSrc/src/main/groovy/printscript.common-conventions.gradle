plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jmailen.kotlinter'
    id 'maven-publish'
}

group 'org.maurokinderknecht.ingsis.printscript'

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/austral-ingsis/printscript-parser-common")
        credentials {
            username = project.findProperty("GITHUB_ACTOR") as String ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("GITHUB_TOKEN") as String ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    implementation 'org.austral.ingsis.printscript:printscript-parser-common:0.1.6'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.6.20"
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

compileKotlin {
    kotlinOptions.jvmTarget = "16"
}

kotlinter {
    disabledRules = ["no-wildcard-imports","no-unused-imports"]
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/MauroKinderknecht/PrintScript")
            credentials {
                username = project.findProperty("GITHUB_ACTOR") as String ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("GITHUB_TOKEN") as String ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.named('build') {
    dependsOn 'lintKotlin'
}
